10 PRINT "ANGLE CONVERSION: RADIANS TO DEGREES"
20 PRINT
30 PRINT "ANGLE IN RADIANS (ENTER 0 TO END PROGRAM)";
40 GOTO 60
50 PRINT "ANGLE IN RADIANS";
60 INPUT R
69 REM - TEST FOR END OF PROGRAM
70 IF R=0 THEN 170
79 REM - CONVERT RADIANS TO SECONDS
80 A=3600*180*R/3.1415927
89 REM - CALCULATE NUMBER OF WHOLE DEGREES
90 D=INT(A/3600)
99 REM - CALCULATE NUMBER OF FULL CIRCLES
100 D1=INT(D/360)
109 REM - CALCULATE DEGREES OF ANGLE WITHIN 360 DEGREES, PRINT
110 PRINT "   DEGREES =";D-360*D1
119 REM - CALCULATE MINUTES, PRINT
120 PRINT "   MINUTES =";INT((A-D*3600)/60)
129 REM - CALCULATE SECONDS, ROUND OFF, PRINT
130 S=A-D*3600-(INT((A-D*3600)/60))*60
140 PRINT "   SECONDS =";INT(100*S+.5)/100
150 PRINT
159 REM - RESTART PROGRAM
160 GOTO 50
170 END
