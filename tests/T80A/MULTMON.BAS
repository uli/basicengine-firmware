1 REM MULTMON  
5 CLS  
10 PRINT  TAB(10); "MONEY MULTIPLIER":PRINT   
20 PRINT "THIS PROGRAM COMPUTES HOW LONG IT WILL TAKE AN INVESTOR  
30 PRINT "TO DOUBLE,TRIPLE, MULTIPLY BY 4, ETC.  HIS/HER MONEY INVESTED   
35 PRINT "AT A KNOWN INTEREST RATE.  
37 PRINT   
40 PRINT "COMPUTATIONS MAY BE DONE WHEN RATES ARE QUOTED ANNUALYY OR  
50 PRINT "PER PERIOD(TIME BETWEEN COMPOUNDINGS). IN THE ANNUAL CASE  
60 PRINT "IT IS NECESSARY TO GIVE THE NUMBER OF TIMES PER YEAR  
70 PRINT "INTEREST IS COMPOUNDED(NUMBER OF INTEREST PERIODS).  
80 PRINT   
90 INPUT "PRESS ENTER TO BEGIN ";Z9$  
100 CLS:CLEAR ' 400  
110 PRINT "      DOUBLE MONEY MENU":PRINT   
115 PRINT "0. QUIT"
120 PRINT "1. INTEREST RATES & TIME MEASURED ANNUALLY"  
130 PRINT "2. INTEREST RATES & TIME MEASURED BY PERIOD  
140 PRINT :INPUT "ENTER YOUR CHOICE";Z9  
145 IF Z9 = 0 THEN END
150 IF  Z9<>1  AND  Z9<>2  THEN  GOSUB  10000:GOTO  100  
160 ON  Z9 GOTO  1000,1500  
1000 REM ANNUAL INTEREST RATE USED  
1010 INPUT "ENTER ANNUAL INTEREST RATE IN %, E.G. 5% ENTERED AS 5";P:P=P/100  
1030 PRINT   
1040 PRINT "WE NOW WANT THE NUMBER OF COMPOUNDING PERIODS PER YEAR.  
1045 PRINT "IF THIS IS DAILY THEN SOME BANKS USE 360 & SOME 365"  
1050 INPUT "ENTER NUMBER";NC  
1052 PRINT :PRINT "ENTER THE FACTOR BY WHICH YOU DESIRE TO INCREASE YOUR FUNDS.  
1053 PRINT "E.G. TO DOUBLE INPUT 2,TRIPLE 3, ETC. ";:INPUT  NF  
1060 NI=LOG(NF)/LOG(1+P/NC):NY=NI/NC  
1080 CLS:PRINT :PRINT :PRINT   
1090 PRINT  "INTEREST RATE & TIME MEASURED ANNUALLY":PRINT   
1100 PRINT  "ANNUAL INTEREST RATE EQUALS ";100*P;"PER CENT"  
1110 PRINT "INTEREST COMPOUNDED ";NC;"TIMES PER YEAR":PRINT   
1120 PRINT "TIME NECESSARY TO MULTIPLY ACCOUNT BY";NF;"EQUALS ";NY; "YEARS"  
1130 PRINT :PRINT "ACTUALLY THIS TAKES BETWEEN ";INT(NI);" AND"; INT(NI)+1;" INTEREST PERIODS  
1140 PRINT "OR BETWEEN ";INT(NI)/NC;" AND";(INT(NI)+1)/NC;" YEARS  
1150 PRINT " THE EXACT AMOUNT OF TIME NEEDED DEPENDS UPON HOW INTEREST  
1160 PRINT "IS CREDITED WITHIN AN INTEREST PERIOD.  
1170 PRINT   
1190 GOTO  2000  
1200 RETURN   
1500 REM INTEREST RATE PER PERIOD USED  
1510 INPUT "ENTER INTEREST RATE PER PERIOD IN % ,E.G. 5% ENTERED AS 5";P: P=P/100  
1515 PRINT :PRINT "ENTER THE FACTOR BY WHICH YOU DESIRE TO INCREASE YOUR FUNDS.  
1517 PRINT "E.G. TO DOUBLE INPUT 2,TRIPLE 3, ETC. ";:INPUT  NF  
1520 N= LOG(NF)/LOG(1+P)  
1522 CLS:PRINT :PRINT :PRINT   
1524 PRINT "INTEREST RATE AND TIME MEASURED PER PERIOD":PRINT   
1526 PRINT "PER PERIOD INTEREST RATE EQUALS ";100*P;"PER CENT"  
1528 PRINT   
1530 PRINT "TIME NEEDED TO MULTIPLY ACCOUNT BY";NF;"EQUALS ";N;" INTEREST PERIODS":PRINT   
1540 PRINT "ACTUALLY THIS TAKES BETWEEN ";INT(N);"AND";INT(N)+1;"INTEREST PERIODS  
1550 PRINT " THE EXACT AMOUNT OF TIME NEEDED DEPENDS UPON HOW INTEREST  
1560 PRINT "IS CREDITED WITHIN AN INTEREST PERIOD."  
1590 PRINT :GOTO  2000  
1610 PRINT   
2000 INPUT "ENTER Y FOR MORE COMPUTATIONS, N TO END ";Z9$  
2010 IF  Z9$<>"Y"  AND  Z9$<>"N"  THEN  GOSUB  10000:GOTO  2000  
2020 IF  Z9$="Y"  THEN  GOTO  100  
2030 END  
10000 PRINT "INPUT ERROR! TRY AGAIN!"  
10010 FOR  JJ=1  TO  300: NEXT  JJ  
10020 RETURN   

