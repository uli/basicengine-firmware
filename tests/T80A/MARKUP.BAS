1 REM MARKUP  
5 CLEAR ' 1000  
10 CLS  
20 PRINT  TAB(15); "MARKUP ANALYSIS":PRINT   
30 PRINT "MARKUP IS THE GROSS PROFIT A BUSINESSMAN MAKES ON AN ITEM  
35 PRINT "EXPRESSED EITHER AS A PER CENT OF COST OR SELLING PRICE.  
40 PRINT "THE PRESENT PROGRAM DETERMINES THE SELLING PRICE GIVEN THE  
45 PRINT "PRICE AND MARKUP. ALSO, GIVEN COST AND SELLING PRICE THE  
50 PRINT "PROGRAM CAN COMPUTE MARKUP BASED ON SELLING PRICE AND COST.  
90 PRINT :INPUT "PRESS ENTER TO BEGIN";Z9$  
95 GOSUB  9000  
100 CLS:PRINT  TAB(10); "MENU":PRINT   
105 PRINT "0. QUIT"  
110 PRINT "1. COMPUTE GROSS MARKUP BASED ON COST"  
120 PRINT "2. COMPUTE GROSS MARKUP BASED ON SELLING PRICE  
130 PRINT "3. COMPUTE GROSS MARKUP GIVEN COST AND SELLING PRICE  
190 PRINT :INPUT "MAKE YOUR CHOICE";Z9  
195 IF Z9 = 0 THEN END
200 ON  Z9 GOSUB  500,600,700  
210 GOTO  100  
500 REM GROSS MARKUP BASED UPON COST  
510 CLS  
520 PRINT "GROSS MARKUP BASED UPON COST":PRINT   
530 INPUT "ENTER COST IN DOLLARS";C  
540 INPUT "ENTER MARKUP AS %    ";M:M=M/100  
550 SP=C*(1+M)  
560 PRINT USING  F$(1);SP  
570 PRINT :GOSUB  10100  
580 RETURN   
600 REM GROSS MARKUP BASED UPON SELLING PRICE  
610 CLS  
620 PRINT "GROSS MARKUP BASED UPON SELLING PRICE":PRINT   
630 INPUT "ENTER COST IN DOLLARS";C  
640 INPUT "ENTER MARKUP AS %    ";M:M=M/100  
650 SP=C/(1-M)  
660 PRINT USING  F$(1);SP  
670 PRINT :GOSUB  10100  
680 RETURN   
700 REM GROSS MARKUP GIVEN COST & SELLING PRICE  
710 CLS:PRINT "GROSS MARKUP GIVEN COST & SELLING PRICE":PRINT   
720 INPUT  "INPUT COST         ";C  
730 INPUT  "INPUT SELLING PRICE";SP  
740 MC=100*(SP/C- 1) : MSP=100*(1 - C/SP)  
750 PRINT   
760 PRINT "GROSS MARKUP BASED UPON COST IS ";:PRINT USING  F$(2);MC;:PRINT  "%"  
770 PRINT   
780 PRINT "GROSS MARKUP BASED UPON SELLING PRICE IS ";:PRINT USING  F$(2);MSP;:PRINT  "%"  
790 PRINT : GOSUB  10100  
795 RETURN   
9000 REM FORMATS  
9001 DIM F$(2)
9010 F$(1)="SELLING PRICE IS    $$#######.##"  
9020 F$(2)="###.###"  
9099 RETURN   
10100 INPUT "PRESS ENTER TO CONTINUE";Z9$:RETURN   

