10 PRINT "PROGRAM FILE 78: ERROR - REFERENCE TO ONE-DIMENSIONAL AND "
15 PRINT "        TWO-DIMENSIONAL VARIABLE OF SAME NAME."
20 PRINT "    ANSI STANDARD 6.4"
30 PRINT
40 PRINT "SECTION 78.1: ERROR - REFERENCE TO ONE-DIMENSIONAL AND "
50 PRINT "        TWO-DIMENSIONAL VARIABLE OF SAME NAME."
60 PRINT
70 PRINT
80 PRINT "THIS IS A TEST FOR A NON-STANDARD FEATURE OF MINIMAL BASIC."
90 PRINT "TO PASS THIS TEST, THE PROCESSOR MUST EITHER:"
100 PRINT
110 PRINT "   1) ACCEPT THE PROGRAM AND BE ACCOMPANIED BY DOCUMENTATION"
120 PRINT "      ACCURATELY DESCRIBING THE FEATURE'S INTERPRETATION"
130 PRINT "      BY THE PROCESSOR, OR"
140 PRINT
150 PRINT "   2) REJECT THE PROGRAM WITH AN APPROPRIATE ERROR MESSAGE"
160 PRINT
170 PRINT "SEE THE NBS MINIMAL BASIC TEST PROGRAMS USER'S MANUAL"
180 PRINT "FOR DETAILED CRITERIA."
190 PRINT
200 PRINT "               BEGIN TEST"
210 PRINT
215 DIM A(10)
220 FOR I=0 TO 10
230 LET A(I)=11*I+5000
240 NEXT I
250 FOR I=0 TO 10
260 FOR J=0 TO 10
270 LET A(I,J)=10000+(100*I)+J
280 NEXT J
290 NEXT I
310 PRINT "VARIABLE A(I) WAS ASSIGNED 11*I + 5000,"
320 PRINT "AND VARIABLE A(I,J) WAS ASSIGNED 10000+(100*I)+J."
330 PRINT "RESULTS BELOW:"
340 PRINT
350 PRINT "ARRAY VALUES FOR A(I,J):"
360 PRINT "EACH LINE HAS ALL THE ELEMENTS FOR A GIVEN VALUE OF I,"
370 PRINT "WITH J GOING FROM 0 TO 10."
380 PRINT
390 LET C1=0
400 LET C2=0
410 FOR I = 0 TO 10
420 PRINT "I=";I;":";
430 FOR J=0 TO 10
440 PRINT A(I,J);
450 IF A(I,J)=10000+(100*I)+J THEN 470
460 LET C2=9
470 NEXT J
480 PRINT
490 NEXT I
500 PRINT
510 PRINT "ARRAY VALUES FOR A(I):"
520 PRINT
530 FOR I = 0 TO 10
540 PRINT "A(";I;") = ";A(I)
550 IF A(I)=11*I+5000 THEN 570
560 LET C1=9
570 NEXT I
580 IF C1=0 THEN 610
590 PRINT
600 PRINT "***** SOME VALUES IN ONE-DIMENSIONAL ARRAY WERE LOST *****"
610 IF C2=0 THEN 640
620 PRINT
630 PRINT "***** SOME VALUES IN TWO-DIMENSIONAL ARRAY WERE LOST *****"
640 PRINT
650 PRINT "               END TEST"
660 PRINT
670 PRINT "END PROGRAM 78"
680 END
